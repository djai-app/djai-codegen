import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

@Suppress("DSL_SCOPE_VIOLATION")
plugins {
    alias(libs.plugins.kotlin.jvm) apply false
    alias(libs.plugins.kotlin.spring) apply false
    alias(libs.plugins.kotlin.jpa) apply false
    alias(libs.plugins.kotlin.allopen) apply false
    alias(libs.plugins.kotlin.noarg) apply false
}

allprojects {
    group = "{{basePackage}}"

    repositories {
        maven { url = uri("https://arti.floliveinfra.net/repository/maven-central/") }
        maven { url = uri("https://arti.floliveinfra.net/repository/maven-releases/") }
        maven { url = uri("https://arti.floliveinfra.net/repository/maven-snapshots/") }

        mavenLocal()
        mavenCentral()
    }

    tasks.withType<Test> {
        useJUnitPlatform()
    }
}

val snakeyamlV2 = libs.versions.snakeyamlV2.get()

subprojects {
    apply {
        plugin("org.jetbrains.kotlin.jvm")
        plugin("org.jetbrains.kotlin.plugin.spring")
        plugin("org.jetbrains.kotlin.plugin.allopen")
        plugin("org.jetbrains.kotlin.plugin.noarg")
        plugin("org.jetbrains.kotlin.plugin.jpa")
    }

    tasks.withType<KotlinCompile> {
        kotlinOptions {
            freeCompilerArgs = listOf("-Xjsr305=strict")
            jvmTarget = "17"
        }
    }

    tasks.withType<JavaCompile> {
        sourceCompatibility = "17"
        targetCompatibility = "17"
    }

    configurations.all {
        resolutionStrategy { force("org.yaml:snakeyaml:$snakeyamlV2") }
    }
}
